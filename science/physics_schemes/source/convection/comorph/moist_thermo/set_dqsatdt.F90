! *****************************COPYRIGHT*******************************
! (C) Crown copyright Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
! *****************************COPYRIGHT*******************************

! Code Owner: Please refer to the UM file CodeOwners.txt
! This file belongs in section: convection_comorph

module set_dqsatdt_mod

implicit none

contains

! Estimates the gradient of the saturation water vapour mixing
! ratio curve as a function of temperature T,
! by assuming d/dT of the saturation vapour pressure follows
! the Claussius-Clapeyron equation:
!
! des/dT = Lc es / (Rv T^2)
!
! p = pd + ev
! qv = rhov/rhod
! pd = rhod Rd T
! ev = rhov Rv T
! => ev/pd = Rv/Rd qv
! => qv = Rd/Rv ev / (p - ev)
!
! Assuming constant total-pressure p,
!
! dqs/dT = dqs/des des/dT
!        = Rd/Rv ( 1/(p - es) + es/(p - es)^2 ) des/dT
!        = Rd/Rv p/(p - es)^2 des/dT
!        = Rd/Rv p/(p - es)^2 Lc es / (Rv T^2)
!        = qs p/(p - es) Lc / (Rv T^2)
!        = qs ( rhod Rd + rhov Rv ) / ( rhod Rd )  Lc / (Rv T^2)
!        = qs ( 1 + Rv/Rd qs ) Lc / (Rv T^2)
!
! This formula is used below...

!----------------------------------------------------------------
! Routine for liquid at all temperatures
!----------------------------------------------------------------
subroutine set_dqsatdt_liq( n_points, temperature, qsat,                       &
                            dqsatdt )

use comorph_constants_mod, only: R_dry, R_vap, real_cvprec, one, sqrt_min_float
use lat_heat_mod, only: set_l_con

implicit none

! Number of points
integer, intent(in) :: n_points

! Temperature
real(kind=real_cvprec), intent(in) :: temperature(n_points)

! Already calculated saturation vapour mixing ratio
! w.r.t. liquid water
real(kind=real_cvprec), intent(in) :: qsat(n_points)

! Partial d/dT of qsat
real(kind=real_cvprec), intent(out) :: dqsatdt(n_points)

! Latent heat of condensation
real(kind=real_cvprec) :: L_con(n_points)

! Loop counter
integer :: ic

! Compute latent heat of condensation
call set_l_con( n_points, temperature, L_con )

! Compute dqsat/dT:
do ic = 1, n_points
  dqsatdt(ic) = qsat(ic) * ( one + (R_vap/R_dry) * qsat(ic) ) * L_con(ic)      &
              / max( R_vap * temperature(ic) * temperature(ic),                &
                     sqrt_min_float )
end do

return
end subroutine set_dqsatdt_liq


!----------------------------------------------------------------
! Routine for ice at all temperatures
!----------------------------------------------------------------
subroutine set_dqsatdt_ice( n_points, temperature, qsat,                       &
                            dqsatdt )

use comorph_constants_mod, only: R_dry, R_vap, real_cvprec, one, sqrt_min_float
use lat_heat_mod, only: set_l_sub

implicit none

! Number of points
integer, intent(in) :: n_points

! Temperature
real(kind=real_cvprec), intent(in) :: temperature(n_points)

! Already calculated saturation vapour mixing ratio
! w.r.t. ice
real(kind=real_cvprec), intent(in) :: qsat(n_points)

! Partial d/dT of qsat
real(kind=real_cvprec), intent(out) :: dqsatdt(n_points)

! Latent heat of sublimation
real(kind=real_cvprec) :: L_sub(n_points)

! Loop counter
integer :: ic

! Compute latent heat of condensation and multiply by it
call set_l_sub( n_points, temperature, L_sub )

! Compute dqsat/dT:
do ic = 1, n_points
  dqsatdt(ic) = qsat(ic) * ( one + (R_vap/R_dry) * qsat(ic) ) * L_sub(ic)      &
              / max( R_vap * temperature(ic) * temperature(ic),                &
                     sqrt_min_float )
end do

return
end subroutine set_dqsatdt_ice


end module set_dqsatdt_mod
